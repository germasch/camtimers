#!/bin/csh
# mira pre-runjob script: 
#  ./pre_runjob.mira with the following optional argument keyword/value pairs, in any order
#   -case <case name>
#   -stdout <standard output from application>
#   -jobid <should be same as $PBS_JOBID>
#   -user <should be same as $LOGNAME>
#   -subdir <directory from which job script was submitted>
#   -rundir <run directory>
#   -save <TRUE|true|anything else>
#   -archive <performance archive root directory>
#   -sampling <sampling interval in seconds>
#   -sample_script <script used to sample system status>
#   -outpe_num <profile_outpe_num setting>
#   -flush_count <mon_flush_count setting>
#   -nprocess <number of MPI processes>  (not used at the current time)
#   -est_remaining <TRUE|true|anything else>

set app_jobid = $COBALT_JOBID

set case = `qstat -lf $app_jobid | grep -F JobName | sed 's/ *JobName *: *\([[:graph:]]*\) */\1/' `
if ("$case" == 'N/A') then
  set case = $app_jobid
endif
set app_stdout = $app_jobid.output

set user = $LOGNAME
set sub_dir = $PWD
set run_dir = $PWD
set save_timing = 'TRUE'
set save_timing_dir = '/projects/FES_HeatFlux'
set sample_interval = 900
set syslog_script = '/projects/FES_HeatFlux/camtimers/xgc_syslog.mira'
set outpe_num = 1
set flush_count = 12
set n_process = 0
set est_remaining = 'FALSE'
set i = 1
while ($i < $#argv)
  if    ("X$argv[$i]" == 'X-case') then
    @ i = $i + 1
    set case = $argv[$i]
  else if ("X$argv[$i]" == 'X-stdout') then
    @ i = $i + 1
    set app_stdout = $argv[$i]
    if (-e $app_stdout) then
      /bin/rm -f $app_stdout
    endif
  else if ("X$argv[$i]" == 'X-jobid') then
    @ i = $i + 1
    set app_jobid = $argv[$i]
  else if ("X$argv[$i]" == 'X-user') then
    @ i = $i + 1
    set user = $argv[$i]
  else if ("X$argv[$i]" == 'X-subdir') then
    @ i = $i + 1
    set sub_dir = $argv[$i]
  else if ("X$argv[$i]" == 'X-rundir') then
    @ i = $i + 1
    set run_dir = $argv[$i]
  else if ("X$argv[$i]" == 'X-save') then
    @ i = $i + 1
    set save_timing = $argv[$i]
  else if ("X$argv[$i]" == 'X-archive') then
    @ i = $i + 1
    set save_timing_dir = $argv[$i]
  else if ("X$argv[$i]" == 'X-sampling') then
    @ i = $i + 1
    set sample_interval = $argv[$i]
  else if ("X$argv[$i]" == 'X-sample_script') then
    @ i = $i + 1
    set syslog_script = $argv[$i]
  else if ("X$argv[$i]" == 'X-outpe_num') then
    @ i = $i + 1
    set outpe_num = $argv[$i]
  else if ("X$argv[$i]" == 'X-flush_count') then
    @ i = $i + 1
    set flush_count = $argv[$i]
  else if ("X$argv[$i]" == 'X-nprocess') then
    @ i = $i + 1
    set n_process = $argv[$i]
  else if ("X$argv[$i]" == 'X-est_remaining') then
    @ i = $i + 1
    set est_remaining = $argv[$i]
  endif
  @ i = $i + 1
end

# set project = `qstat -lf $app_jobid | grep -F Project | sed 's/ *Project *: *\([[:graph:]]*\) */\1/' `

if (($save_timing == 'TRUE') || ($save_timing == 'true')) then

  if (-d $save_timing_dir) then

    cd $save_timing_dir

    if !(-d performance_archive) then
      mkdir performance_archive
      chmod 777 performance_archive
      chmod a+s performance_archive
    endif
    cd performance_archive
    if !(-d $user) then
      mkdir $user
      chmod g+w  $user
      chmod a+rX $user
    endif
    cd $user
    if !(-d $case) then
      mkdir $case
      chmod g+w  $case
      chmod a+rX $case
    endif
    cd $case
    if !(-d $app_jobid) then
      mkdir $app_jobid
      chmod g+w  $app_jobid
      chmod a+rX $app_jobid
    endif
    cd $app_jobid

    if !(-e job_start) then
      touch job_start
      qstat -f > qstatf.$app_jobid
      qstat -lf $app_jobid > qstatf_jobid.$app_jobid
      chmod g+w  *
      chmod a+rX *

      gzip qstatf.$app_jobid qstatf_jobid.$app_jobid 
    endif

    if !(-d checkpoints) then
      mkdir checkpoints
    endif
    cp --preserve=timestamps $run_dir/input checkpoints/input.$app_jobid
    chmod -R g+w  checkpoints
    chmod -R a+rX checkpoints

  endif

endif

cd $run_dir

set string = `qstat -lf $app_jobid | grep -F SubmitTime`
echo "job $app_jobid submitted: $string" >>& CaseStatus.$app_jobid

set string = `qstat -lf $app_jobid | grep -F StartTime`
echo "job $app_jobid started: $string" >>& CaseStatus.$app_jobid

if (-e xgc_mpi_init) then
  /bin/rm -f xgc_mpi_init
endif

if !(-d timing) then
  mkdir timing
endif

if !(-d timing/checkpoints) then
  mkdir timing/checkpoints
endif

chmod -R g+w  timing
chmod -R a+rX timing

if (($est_remaining == 'TRUE') || ($est_remaining == 'true')) then
  set est_rem = 1
else
  set est_rem = 0
endif

csh $syslog_script $sample_interval $n_process $app_jobid $run_dir $app_stdout $run_dir/timing/checkpoints $save_timing_dir/performance_archive/$user/$case/$app_jobid/checkpoints $est_rem &
set syslog_jobid = $!

cat >> syslog_jobid.$app_jobid << EOF1
$syslog_jobid
EOF1

if (-e perf_in) then
  mv perf_in perf_in.$app_jobid
endif

cat >> perf_in << EOF1
&prof_inparam
profile_papi_enable=.true.
profile_single_file = .false.
profile_global_stats = .true.
profile_outpe_num = $outpe_num
/
&papi_inparam
/

EOF1

if (-e mon_in) then
  mv mon_in mon_in.$app_jobid
endif

cat >> mon_in << EOF1
&mon_param
mon_flush_count = $flush_count
/

EOF1

set sdate = `date +"%Y-%m-%d %H:%M:%S"`
echo "runjob started $sdate" >>& CaseStatus.$app_jobid
echo "`date` -- APPLICATION EXECUTION BEGINS HERE" 

if ($est_rem > 0) then
set remaining = 0
set TimeRemaining = `qstat -lf $app_jobid | grep -F TimeRemaining | sed 's/^ *TimeRemaining *: *\([0-9]*:[0-9]*:[0-9]*\) */\1/' `
set rem_hours     = `echo $TimeRemaining | sed 's/^0*\([0-9]*\):0*\([0-9]*\):0*\([0-9]*\)/\1/' `
set rem_mins      = `echo $TimeRemaining | sed 's/^0*\([0-9]*\):0*\([0-9]*\):0*\([0-9]*\)/\2/' `
set rem_secs      = `echo $TimeRemaining | sed 's/^0*\([0-9]*\):0*\([0-9]*\):0*\([0-9]*\)/\3/' `
if ("X$rem_hours" == "X") set rem_hours = 0
if ("X$rem_mins" == "X")  set rem_mins  = 0
if ("X$rem_secs" == "X")  set rem_secs  = 0
@ remaining = 3600 * $rem_hours + 60 * $rem_mins + $rem_secs
cat > $run_dir/Walltime.Remaining << EOF1
&rem_param
rem_walltime = $remaining
rem_walltime_src = 2
/

EOF1
endif

